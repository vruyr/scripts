#!/usr/bin/env zsh

if [ $# -eq 1 -a "$1" = "--known-hosts" ]; then
	cat ~/.ssh/known_hosts
	exit 0
fi

local the_password="1"
askpass_expected_suffix="'s password: "
if [ $# -eq 1 -a "${1:(-${#askpass_expected_suffix})}" = "$askpass_expected_suffix" ]; then
	printf >&2 '\x1b[0mLogging into \x1b[32m%q\x1b[0m using password \x1b[32m%q\x1b[0m\n' "${1:0:(-${#askpass_expected_suffix})}" "${the_password}"
	# Assuming the password is "1"
	echo "${the_password}"
	exit 0
fi

export SSH_ASKPASS="${0:A}"
export SSH_ASKPASS_REQUIRE='force'
ssh_opts=(
	# Only fail non-matching host keys for hosts we have logged in before
	-o StrictHostKeyChecking=accept-new
	# Do not update the known_hosts file
	-o UserKnownHostsFile=/dev/null
	# But use the existing known_hosts to verify already visited hosts
	-o KnownHostsCommand="${0:A} --known-hosts"
	# And be quiet about it
	-o LogLevel=ERROR
)
exec ssh "${ssh_opts[@]}" "$@"
