#!/usr/bin/env zsh

vscode_process_applescript='
	tell application "System Events"
		return POSIX path of application file of first process whose bundle identifier is "com.microsoft.vscode"
	end tell
'

fzf_args=(
	--wrap --reverse --exit-0
	--header="VS Code does not seem to be running. Select which one to launch: "
	--header-first
	--height 40%
	--min-height 18+
	--preview='
		mdls -name kMDItemDisplayName -name kMDItemVersion {} ;
		echo --- ;
		codesign --display --verbose=2 --check-notarization {} ;
	'
	--preview-window='right,50%'
)

if ! vscode_bundle_path="$(osascript 2>/dev/null -e "$vscode_process_applescript")"; then
	vscode_bundle_path="$(
		mdfind 'kMDItemCFBundleIdentifier == "com.microsoft.vscode"c' \
		| fzf "${fzf_args[@]}"
	)"
	fzf_exit_code=$?
	if [[ "$fzf_exit_code" -eq 130 ]]; then
		# Interrupted with Ctrl-C or Esc
		exit 130
	elif [[ "$fzf_exit_code" -ne 0 ]] || ! [[ -d "$vscode_bundle_path" ]]; then
		echo "Failed to find VS Code." 1>&2
		exit 127
	fi
fi

exec "$vscode_bundle_path/Contents/Resources/app/bin/code" "$@"
