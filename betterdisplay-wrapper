#!/usr/bin/env zsh

set -o errexit

hash BetterDisplay="$(osascript -e 'tell application "System Events" to return POSIX path of application file of first process whose bundle identifier is "pro.betterdisplay.BetterDisplay"')/Contents/MacOS/BetterDisplay"
hash jq="${${:-"jq"}:c}"

for param in -autoBrightness -nightShift -trueTone; do
	printf '%s %s\n' "$param" "$(BetterDisplay 2>&1 get "$param")"
done
echo

for display in "${(@f)"$(BetterDisplay get -identifiers | sed 's/},{/}\n{/' | jq -s 'sort_by(.vendor, .model, .serial)|.[]' -c)"}"; do
	display_selector=(
		-vendor="$(jq -r <<<"$display" .vendor)"
		-model="$(jq -r <<<"$display" .model)"
		-serial="$(jq -r <<<"$display" .serial)"
	)
	display_label="$(jq <<<"$display" -r '"\(.name) \(.alphanumericSerial)"') (${(j: :)display_selector[@]})"
	if [ "$#" -ge 1 ]; then
		if [[ "$display_label" = *"$1"* ]]; then
			:
		else
			continue
		fi
	fi

	printf '%s\n' "$display_label"

	if [ "$(jq <<<"$display" -r '"\(.vendor):\(.name)"')" = "1552:Built-in Display" ]; then
		# MacBook built-in displays do not support DDC.
		for param in -xdrPreset -hardwareBrightness; do
			printf '  %-30s -value=%q\n' "$param" "$(BetterDisplay 2>&1 "${display_selector[@]}" get "$param")"
		done
	else
		for param in -colorProfileURL; do
			printf '  %-30s -value=%s\n' "$param" "$(BetterDisplay get "${display_selector[@]}" "$param")"
		done
		for vcp in selectColorPreset ; do
			params=(-ddc -vcp="$vcp")
			printf '  %-30s -value=%s\n' \
				"${(j: :)params[@]}" \
				"$(BetterDisplay 2>&1 "${display_selector[@]}" "${params[@]}" get -value)"
		done
		for vcp in luminance contrast videoGainRed videoGainGreen videoGainBlue; do
			params=(-ddc -vcp="$vcp")
			printf '  %-30s -value=%s -max=%s\n' \
				"${(j: :)params[@]}" \
				"$(BetterDisplay 2>&1 "${display_selector[@]}" "${params[@]}" get -value)" \
				"$(BetterDisplay 2>&1 "${display_selector[@]}" "${params[@]}" get -max)"
		done
	fi
	echo
done
