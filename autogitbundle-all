#!/usr/bin/env zsh

prefixes=()
for p in "${(u@)${(ps:\0:):-"$(git config --null --get-regex '^autogitbundle\..*\.prefix')"}#*$'\n'}"; do
	if [[ "$p" == "~" ]]; then
		prefixes+=( "${HOME%/}" )
	elif [[ "${p:(-1)}" == "/" ]]; then
		p="${p%/}"
		prefixes+=( ${~:-"$p"} )
	else
		prefixes+=( ${~:-"${p}*/"} )
	fi
done
prefixes=( "${(u)prefixes[@]%/}" )

typeset -A repos
repos=( ${(f)"$(
	find "${prefixes[@]}" -type d -name objects -execdir zsh -c '
		printf >&2 '.'
		[[ -f config && -f HEAD ]] || exit
		echo "${${"$(git config --get core.worktree)":-${PWD%/.git}}:A}"
		echo "${"$(git rev-parse --git-path .)":A}"
	' \;
)"} )

printf >&2 '\n'

for worktree gitdir in "${(@kv)repos}"; do
	(cd "$worktree" && export GIT_DIR="$gitdir" && autogitbundle)
	(cd "$gitdir" && autogitbundle)
done
