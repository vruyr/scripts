#!/usr/bin/env zsh

#
# Note:
#
# This script will use `pretty.fzf` for log format, if one is configured in git.
#
# The git log format should contain two fields separated by Unit Separator (U+001F):
#
# 1. The commit itself that fzf will show to the user.
# 2. The commit hash that fzf will use to return to the user and use in the preview command.
#
# Individual commits will be separated by a NUL character (U+0000) so newlines are supported.
#

git_log_format_default='tformat:%h% t% ad% aE%+s%+D%n%x1F%H'

git_log_args=(
	--graph
	-z
	--pretty="${"${"$(git config --get pretty.fzf)":+"fzf"}":-"$git_log_format_default"}"
	"$@"
)

typeset -A git_configs_kv
git_configs_kv=(
	[color.ui]='always'
)
git_configs=()
for key value in "${(@kv)git_configs_kv}"; do
	git_configs+=( "-c" "$key=$value" );
done
unset git_configs_kv


preview_command='
	git show \
		--format=fuller \
		--date="format-local:%a %m/%d/%Y %H:%M:%S %z" {2} \
	| \
	delta --width=$FZF_PREVIEW_COLUMNS
'

#TODO Delta fails to detect terminal colors from within fzf. Fix it.

fzf_args=(
	--read0
	--delimiter $'\x1F'
	--with-nth 1
	--accept-nth 2

	--reverse
	--no-sort
	--ansi
	--wrap
	--highlight-line

	--with-shell 'zsh -c'

	--bind 'resize:refresh-preview'
	--preview-window wrap
	--preview "$preview_command"
)

git "${git_configs[@]}" log "${git_log_args[@]}" | fzf "$fzf_args[@]"
